plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.9' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.google.cloud.tools.jib' version '3.4.1' apply false
}

allprojects {
    group = 'com.groom'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    // 서비스 모듈들에만 Jib 설정 적용
    if (project.path.startsWith(":service:")) {
        apply plugin: "com.google.cloud.tools.jib"

        jib {
            from{
                image = 'amazoncorretto:17-alpine'
                auth{
                    username=''
                    password=''
                }
            }

            container {
                jvmFlags = [
                        '-Duser.timezone=Asia/Seoul',
                        '-Xms256m',
                        '-Xmx512m'
                ]
                ports = ['8080']
                format = 'OCI'
            }
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    // 모든 서브 프로젝트의 bootJar를 기본적으로 비활성화
    bootJar {
        enabled = false
    }

    // 일반 jar 생성은 허용 (common 같은 모듈을 위해)
    jar {
        enabled = true
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        set('springCloudVersion', "2025.0.1")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'
    }

    test {
        useJUnitPlatform {
            excludeTags 'Integration'
        }
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

// 실행 가능한 BootJAR 생성 프로젝트 리스트
def bootableProjects = [
        project(':service:user'),
        project(':service:cart'),
        project(':service:order'),
        project(':service:payment'),
        project(':service:product')
]

configure(bootableProjects) {
    bootJar {
        enabled = true
    }
    jar {
        enabled = false
    }
    dependencies {
        implementation project(':service:common')
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'
    }
}